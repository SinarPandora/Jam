plugins {
    id "idea"
    id "java"
    id "scala"
    id "distribution"
    id "application"
    id "java-library"
    id "com.github.maiflai.scalatest" version "$scala_test_plugin_version"
    id "io.freefair.lombok" version "5.3.0"
}

sourceCompatibility = "11"

scala {
    zincVersion = "1.3.5"
}

distributions {
    main {
        distributionBaseName = "jam_self"
        contents {
            into("conf") {
                from "conf/."
                exclude "*test*"
                exclude "*local*"
            }
            into("backend") {
                from "../jam_mirai_backend/mirai_console/backend.jar"
            }
        }
    }
}

[distZip, distTar]*.dependsOn tasks.findByPath(':jam_mirai_backend:downloadBackend')

application {
    mainClass = 'o.lartifa.jam.Bootloader'
    applicationDefaultJvmArgs = ["-Xmx1025m", "-Xms512m", "-ea", "-server", "-Dfile.encoding=UTF-8"]
}

startScripts {
    classpath = files('$APP_HOME/lib/*')
    doLast {
        def windowsScriptFile = file getWindowsScript()
        def unixScriptFile = file getUnixScript()
        windowsScriptFile.text = windowsScriptFile
                .text.replace('set CLASSPATH=%APP_HOME%\\lib\\*',
                'set CLASSPATH=%APP_HOME%\\lib\\*;%APP_HOME%\\plugins\\*')
        unixScriptFile.text = unixScriptFile
                .text.replace('CLASSPATH=$APP_HOME/lib/*',
                'CLASSPATH=$APP_HOME/lib/*:$APP_HOME/plugins/*')
    }
}

repositories {
    maven { url "https://jitpack.io" }
    jcenter()
    mavenCentral()
}

dependencies {
    // Scala
    api("org.scala-lang:scala-library:$scala_version")

    // Language extensions
    api("com.chuusai:shapeless_2.13:2.3.3")
    api("org.scala-lang:scala-reflect:$scala_version")
    api("org.scala-lang.modules:scala-async_2.13:0.10.0")
    api("org.scala-lang.modules:scala-parallel-collections_2.13:0.2.0")

    // Core
    api(project(":picq_bot_x_fork"))

    // Meta
    api("com.jayway.jsonpath:json-path:2.4.0")
    api("org.codehaus.groovy:groovy-all:$groovy_version")

    // Database
    api("com.typesafe.slick:slick_2.13:$slick_version")
    api("com.typesafe.slick:slick-codegen_2.13:$slick_version")
    api("com.typesafe.slick:slick-hikaricp_2.13:$slick_version")
    api("com.h2database:h2:1.4.200")
    api("org.postgresql:postgresql:42.2.14")
    api("org.flywaydb:flyway-core:6.5.5")

    // Tools
    api("com.apptastic:rssreader:2.2.3")
    api("org.reflections:reflections:0.9.12")
    api("io.reactivex.rxjava3:rxjava:$rx_extension_version")
    api("com.lihaoyi:ammonite-ops_2.13:2.1.4")
    api("com.github.pathikrit:better-files_2.13:3.9.1")
    api("com.lihaoyi:requests_2.13:0.6.2")
    api("com.lihaoyi:upickle_2.13:1.2.2")
    api("com.lihaoyi:ujson_2.13:1.2.2")
    api("org.jsoup:jsoup:1.13.1")
    api("net.redhogs.cronparser:cron-parser-core:3.5")
    api("com.sksamuel.scrimage:scrimage-core:4.0.6") {
        exclude group: "ch.qos.logback", module: "logback-classic"
    }
    implementation("org.slf4j:slf4j-simple:1.7.30")

    // Test
    testImplementation("org.scalactic:scalactic_2.13:$scala_test_version")
    testImplementation("org.scalatest:scalatest_2.13:$scala_test_version")
    testImplementation("org.scalamock:scalamock_2.13:4.4.0")
    testRuntime("com.vladsch.flexmark:flexmark-all:0.35.10")
}
